plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'signing'
    id 'distribution'
}

sourceCompatibility = 1.8
compileJava.options.encoding = 'utf-8'
group = "io.github.9ins"
archivesBaseName = "console-input-manager"
version = "1.0.2"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'console-input-manager'
            version = project.version
            pom {
                name = 'console-input-manager'
                description = 'This library helps you build Java console input processes in your console application with the easiest way of development. It provides an effective and easy way to develop console applications by defining interactive queries that are needed in the application as user input.'
                url = 'https://github.com/9ins/console-input-manager'

                scm {
                    connection = 'scm:git:https://github.com/9ins/console-input-manager.git'
                    developerConnection = 'scm:git:https://github.com/9ins/console-input-manager.git'
                    url = 'https://github.com/9ins/console-input-manager.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'chaos930'
                        name = 'Kooin-Shin'
                        email = 'chaos930@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = uri('https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/')
            credentials {
                username = project.findProperty('ossrhUsername') ?: ""
                password = project.findProperty('ossrhPassword') ?: ""
            }
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': archivesBaseName,
                   'Implementation-Version': version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'utf-8'
}

repositories {
    jcenter()
}

dependencies {
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.26'
}
